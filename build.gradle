buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:4.0.0-beta"
    }
}

plugins {
    id "checkstyle"
    id "org.springframework.boot" version "2.0.8.RELEASE"
}

ext {
    swagger_annotations_version = "1.5.21"
    jackson_version = "2.8.11"
    jackson_databind_version = "2.8.11.2"
    jersey_version = "2.27"
    spring_version = "5.1.4.RELEASE"
    spring_boot_version = "2.1.2.RELEASE"
    junit_version = "5.3.2"
    slf4j_version = "1.7.25"
}

allprojects {
    apply plugin: "java"
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    checkstyle {
        toolVersion "8.16"
    }

    group = "org.candlepin"
}

version = "1.0.0"

bootJar {
    mainClassName = "org.candlepin.insights.BootApplication"
}

processResources {
    filesMatching("**/application.yaml") {
        expand(project.properties)
    }
}

dependencies {
    // Generates configuration metadata that IntelliJ can use
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:$spring_boot_version"

    compile project(":api")
    compile project(":insights-inventory-client")
    compile project(":pinhead-client")
    compile "javax.servlet:javax.servlet-api:3.1.0"
    compile "org.jboss.spec.javax.ws.rs:jboss-jaxrs-api_2.1_spec:1.0.2.Final"
    compile "org.springframework.boot:spring-boot-actuator-autoconfigure:$spring_boot_version"
    compile "org.springframework.boot:spring-boot-autoconfigure:$spring_boot_version"
    compile "org.springframework.boot:spring-boot:$spring_boot_version"
    compile "org.springframework:spring-beans:$spring_version"
    compile "org.springframework:spring-context:$spring_version"
    compile "org.springframework:spring-webmvc:$spring_version"
    compile "org.slf4j:slf4j-api:$slf4j_version"
    compile "net.logstash.logback:logstash-logback-encoder:5.3"
    compile "io.micrometer:micrometer-registry-prometheus:1.1.2"
    compile "org.springframework.boot:spring-boot-starter-jersey:$spring_boot_version"

    // Runtime deps that will be included in the result package but not on the compile classpath.  I.e.
    // implementations of APIs we are using.
    runtime "ch.qos.logback:logback-classic:1.2.3"
    runtime "org.hibernate.validator:hibernate-validator:6.0.14.Final"

    testCompile "org.springframework.boot:spring-boot-test:$spring_boot_version"
    testCompile "org.springframework:spring-test:$spring_version"
    testCompile "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testCompile "org.mockito:mockito-core:2.23.4"
    testCompile "org.mockito:mockito-junit-jupiter:2.23.4"
}

compileJava.dependsOn(processResources)

project(":api") {
    apply plugin: "org.openapi.generator"

    // This project should only contain generated code.  No point in running checkstyle
    checkstyle.sourceSets = []

    ext {
        api_spec_path = "${projectDir}/rhsm-conduit-api-spec.yaml"
        config_file = "${projectDir}/rhsm-conduit-api-config.json"
    }

    openApiGenerate {
      generatorName = "jaxrs-spec"
      inputSpec = api_spec_path
      configFile = config_file
      outputDir = "$buildDir/generated"
      configOptions = [
          interfaceOnly: true,
          generatePom: false
      ]
    }

    openApiValidate {
        inputSpec = api_spec_path
    }

    task generateApiDocs(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
        generatorName = "html"
        inputSpec = api_spec_path
        outputDir = "$buildDir/docs"
    }

    dependencies {
        compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.8'
        compile 'javax.validation:validation-api:2.0.1.Final'
        compile 'org.jboss.spec.javax.ws.rs:jboss-jaxrs-api_2.1_spec:1.0.2.Final'
        compile 'io.swagger:swagger-annotations:1.5.21'
    }

    sourceSets.main.java.srcDirs = ["${buildDir}/generated/src/gen/java"]
    compileJava.dependsOn tasks.openApiGenerate
}

project(":insights-inventory-client") {
    apply plugin: "org.openapi.generator"

    ext {
        api_spec_path = "https://raw.githubusercontent.com/RedHatInsights/insights-host-inventory/master/swagger/api.spec.yaml"
        config_file = "${projectDir}/insights-inventory-client-config.json"
    }
}

project(":pinhead-client") {
    apply plugin: "org.openapi.generator"

    ext {
        api_spec_path = "${projectDir}/pinhead-client-spec.yaml"
        config_file = "${projectDir}/pinhead-client-config.json"
    }

    dependencies {
        testCompile "com.github.tomakehurst:wiremock-jre8:2.21.0"
    }
}

configure(subprojects.findAll { it.name == 'insights-inventory-client' || it.name == 'pinhead-client'}) {
    apply plugin: "org.openapi.generator"

    openApiValidate {
        inputSpec = api_spec_path
    }

    task generateApiDocs(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
        generatorName = "html"
        inputSpec = api_spec_path
        outputDir = "$buildDir/docs"
    }

    openApiGenerate {
        inputSpec = api_spec_path
        configFile = config_file
        outputDir = "${buildDir}/generated"
        generatorName = "java"
        configOptions = [
            generatePom: false,
            library: "jersey2",
            java8: true,
            dateLibrary: "java8"
        ]
    }

    dependencies {
        compile "org.glassfish.jersey.core:jersey-client:$jersey_version"
        compile "org.glassfish.jersey.inject:jersey-hk2:$jersey_version"
        compile "org.glassfish.jersey.media:jersey-media-json-jackson:$jersey_version"
        compile "org.glassfish.jersey.media:jersey-media-multipart:$jersey_version"
        compile "org.apache.httpcomponents:httpclient:4.5.7"

        compile "com.fasterxml.jackson.core:jackson-databind:$jackson_databind_version"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
        compile "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"

        compile "org.springframework:spring-context:$spring_version"
        compile "org.slf4j:slf4j-api:$slf4j_version"
        compile "io.swagger:swagger-annotations:$swagger_annotations_version"

        testCompile "org.springframework:spring-test:$spring_version"
        testCompile "org.springframework.boot:spring-boot-test:$spring_boot_version"
        testCompile "org.junit.jupiter:junit-jupiter-api:$junit_version"
        testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit_version"
        testRuntime "ch.qos.logback:logback-classic:1.2.3"
    }

    sourceSets.main.java.srcDirs += "${buildDir}/generated/src/main/java"
    compileJava.dependsOn tasks.openApiGenerate
}
